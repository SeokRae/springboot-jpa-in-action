buildscript {
    ext {
        springBootVersion = '2.3.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    }
}


subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    group 'kr.seok'
    version '1.0-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        /* Web */
        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        /* Jpa */
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        /* db (jpa 설정 시 기본 db가 존재해야 함 */
        runtimeOnly 'com.h2database:h2'

        /* lombok */
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

    }
}


/* Querydsl 프로젝트 리스트 */
def querydslProjects = [
        project(":springboot-jpa-shop-optimize"),
        project(":springboot-jpa-querydsl"),
        project(":springboot-jpa-querydsl-basic")
]

configure(querydslProjects) {

    apply plugin: "io.spring.dependency-management"

    dependencies {
        compile "com.querydsl:querydsl-jpa"
        compile "com.querydsl:querydsl-core"
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
        annotationProcessor "jakarta.persistence:jakarta.persistence-api:2.2.3"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api:1.3.5"
    }

    // querydsl 적용
    def generated = "$buildDir/main/generated"

    /* 클래스 파일을 해당 폴더에 생성 하도록 경로 설정 */
    sourceSets {
        main.java.srcDirs += [ generated ]
    }

    tasks.withType(JavaCompile) {
        options.annotationProcessorGeneratedSourcesDirectory = file(generated)
    }

    clean.doLast {
        file(generated).deleteDir()
    }
}

project(":springboot-jpa-shop") {
    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            /* JUnit 5를 사용하는 경우 Junit4 내용을 제거해줘야 함, (이하 버전 사용하려는 경우 주석처리) */
//            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}
project(":springboot-jpa-shop-optimize") {
    dependencies {
        /* entity lazyloading 해결을 위해서 해당 모듈을 등록하는것 잘못된 해결법 */
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // QueryDSL
        implementation 'com.querydsl:querydsl-jpa'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            /* JUnit 5를 사용하는 경우 Junit4 내용을 제거해줘야 함, (이하 버전 사용하려는 경우 주석처리) */
//            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(":springboot-jpa-data") {
    dependencies {
        implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.7'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            /* JUnit 5를 사용하는 경우 Junit4 내용을 제거해줘야 함, (이하 버전 사용하려는 경우 주석처리) */
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
    /* JUnit5를 사용하는 경우 설정 */
    test {
        useJUnitPlatform()
    }
}

project(":springboot-jpa-querydsl") {
    dependencies {
        /* QueryDSL 라이브러리로 의존관계 추가 */
        implementation 'com.querydsl:querydsl-jpa'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            /* JUnit 5를 사용하는 경우 Junit4 내용을 제거해줘야 함, (이하 버전 사용하려는 경우 주석처리) */
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
    /* JUnit5를 사용하는 경우 설정 */
    test {
        useJUnitPlatform()
    }

}
